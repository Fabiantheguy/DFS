boolean netThrown = false;
boolean netLanded = false;
float netX, netY, netStartX, netStartY;
float boatX = 300, boatY = 400;
float boatTargetX = boatX;
boolean movingLeft = false, movingRight = false;
int netMax = 4; // Amount of cans that fit in the net
float boatSpeed = 5; // Boat's movement speed

// Net movement variables
float netDirX, netDirY;
float netSpeed = 5;
float netDistance = 0;
float maxNetSize = 50; // Max size of the net
float netSize; // Actual size of the net
float targetDistance; // The actual distance the net will travel
float maxRange = 150;  // Maximum net throwing distance
boolean isRetracting = false;

// Velocity-based movement for smooth retraction
float netVelocityX = 0;
float netVelocityY = 0;
float deceleration = 0.95; // Gradual slowdown effect
float fallSpeed = 0.5; // Speed at which the net falls down
// Boat movement setup
void boatSetup() {
  boatX = width / 2;
  boatY = height - 500;
  boatTargetX = boatX; // Set initial target to starting position
}

void boatDraw() {
  move(); // Move the boat according to player input
  boatX = lerp(boatX, boatTargetX, 0.1); // Smooth transition for boat
  drawBoat(); // Draw the boat
  retract(); // Handle net retraction when needed

  // Calculate the distance between the boat and the net to handle its movement
  float distanceToBoat = dist(netX, netY, boatX, boatY);
  if (netThrown) {
    if (!netLanded) {
      // Move net forward and shrink it
      if (netDistance < targetDistance) {
        netX += netDirX * netSpeed;
        netY += netDirY * netSpeed;
        netDistance += netSpeed;
        netSize = map(netDistance, 0, targetDistance, 20, maxNetSize);
      } else {
        netLanded = true;
      }
    } else {
      if (distanceToBoat > maxRange) {
        // Allow net to drift smoothly toward the boat
        netVelocityX += (boatX - netX) * 0.002;
        if (netY < boatY) {
          netVelocityY += (boatY - netY) * 0.002;
        }
      } else {
        // Let the net slowly fall down until it reaches max range
        netY += fallSpeed;
      }

      // Apply velocity and gradual slowdown
      netVelocityX *= deceleration;
      netVelocityY *= deceleration;
      netX += netVelocityX;
      netY += netVelocityY;

      // Constrain net position within river bounds at its Y position
      constrainNetPosition();
      
      // Reset net when it reaches the boat
      if (distanceToBoat < 50) {
        netLanded = false; // Ready for a new throw
        netThrown = false;
        isRetracting = false;
        netSize = 0; // Reset net size
      }
    }

    drawNet(); // Draw the net
  }
}

class MoveLeftCommand implements Command {
  public void execute() {
    movingLeft = true;
  }
}

class MoveRightCommand implements Command {
  public void execute() {
    movingRight = true;
  }
}

class StopMoveLeftCommand implements Command {
  public void execute() {
    movingLeft = false;
  }
}

class StopMoveRightCommand implements Command {
  public void execute() {
    movingRight = false;
  }
}

class ThrowNetCommand implements Command {
  public void execute() {
    if (!netThrown) {
      netThrown = true;
      netX = boatX;
      netY = boatY;
      netStartX = boatX;
      netStartY = boatY;
      netDistance = 0;
      netSize = 50;

      float dirX = mouseX - boatX;
      float dirY = mouseY - boatY;
      float length = sqrt(dirX * dirX + dirY * dirY);
      targetDistance = min(length, maxRange);
      netDirX = dirX / length;
      netDirY = dirY / length;

      // Constrain net's starting X position within river bounds
      float xOffset = sin((netY + riverOffset) * waveFrequency) * waveAmplitude;
      float leftBound = riverX + xOffset + 10;
      float rightBound = riverX + xOffset + riverWidth - 10;
      netX = constrain(netX, leftBound, rightBound);
    }
    if (netThrown && netLanded) {
      isRetracting = true;
    }
  }
}

// Boat movement constraints based on the dynamic width of the river
// Boat movement constraints based on the dynamic width of the river
void move() {
  if (movingLeft) {
    boatTargetX -= boatSpeed;
  }
  if (movingRight) {
    boatTargetX += boatSpeed;
  }

  // Get dynamic river width at boat's Y position
  float dynamicWaveAmplitude = waveAmplitude + sin(boatY * 0.001) * waveVariation;
  float dynamicWidth = riverWidth + sin((boatY + riverOffset) * 0.001) * widthVariation;
  float xOffset = sin((boatY + riverOffset) * (waveFrequency + directionBias)) * dynamicWaveAmplitude;
  
  // Calculate the left and right river boundaries at boatY, considering the river width and wave offset
  float leftBound = riverX + xOffset - dynamicWidth / 2 + 25; // Add margin and account for boat width
  float rightBound = riverX + xOffset + dynamicWidth / 2 + 15; // Add margin and account for boat width

  boatTargetX = constrain(boatTargetX, leftBound, rightBound); // Constrain boat movement within river bounds
}

void boatMousePressed() {
  if (!netThrown) {
    netThrown = true;
    netX = boatX;
    netY = boatY;
    netStartX = boatX;
    netStartY = boatY;

    netDistance = 0;  // Reset net travel distance
    netSize = 50;  // Reset net size

    // Calculate distance to the mouse
    float dirX = mouseX - boatX;
    float dirY = mouseY - boatY;
    float length = sqrt(dirX * dirX + dirY * dirY);

    // Set the actual target distance (cap at maxRange)
    targetDistance = min(length, maxRange);

    // Normalize direction
    netDirX = dirX / length;
    netDirY = dirY / length;
  }
  if (netThrown && netLanded) {
    isRetracting = true;
  }
}

void retract() {
  if (isRetracting) {
    netVelocityX += (boatX - netX) * 0.002;
    netVelocityY += (boatY - netY) * 0.002;
    netVelocityX *= deceleration;
    netVelocityY *= deceleration;
    netX += netVelocityX;
    netY += netVelocityY;
  }
}
// Draw the boat
// Draw the boat
void drawBoat() {
  fill(139, 69, 19);
  stroke(200, 50, 10);
  strokeWeight(2);

  float w = 40;
  float h = 60;

  beginShape();
  vertex(boatX - w / 2, boatY + h / 2);
  vertex(boatX + w / 2, boatY + h / 2);
  vertex(boatX + w * 0.7 / 2, boatY - h / 2);
  vertex(boatX - w * 0.7 / 2, boatY - h / 2);
  endShape(CLOSE);
}

// Net movement and drawing
void drawNet() {
  stroke(255);
  fill(200);
  strokeWeight(2);

  // Draw tether from boat to net
  line(boatX, boatY, netX, netY);

  // Draw web structure dependent on maximum amount of cans
  ellipse(netX, netY, netSize, netSize);
  for (int i = 0; i < netMax; i++) {
    float angle = TWO_PI / netMax * i;
    float x = netX + cos(angle) * netSize / 2;
    float y = netY + sin(angle) * netSize / 2;
    line(netX, netY, x, y);
  }
}

// Constrain net position based on dynamic river width at net's Y position
void constrainNetPosition() {
  // Calculate dynamic wave effects for net
  float dynamicWaveAmplitude = waveAmplitude + sin(netY * 0.001) * waveVariation;
  float dynamicWidth = riverWidth + sin((netY + riverOffset) * 0.001) * widthVariation;
  float xOffset = sin((netY + riverOffset) * (waveFrequency + directionBias)) * dynamicWaveAmplitude;

  // Calculate left and right boundaries of the river at net's Y position
  float leftBound = riverX + xOffset + 10 + netSize / 2; // Adding margin and half net size
  float rightBound = riverX + xOffset + riverWidth - 10 - netSize / 2; // Adding margin and half net size

  // Constrain the net's center (netX) within the left and right bounds of the river
  netX = constrain(netX, leftBound, rightBound);

  // Ensure the net stays within the visible river and doesn't go out of the background ("blue")
  netX = constrain(netX, riverX + 10 + netSize / 2, riverX + riverWidth - 10 - netSize / 2);
}
